import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()

        maven {
            name("Fabric")
            url("https://maven.fabricmc.net")
        }

        maven {
            name("Architectury")
            url("https://maven.architectury.dev/")
        }

        maven {
            name("MinecraftForge")
            url("https://files.minecraftforge.net/maven/")
        }

        maven {
            name("NeoForge")
            url("https://maven.neoforged.net/releases/")
        }

        maven {
            name("Jitpack")
            url("https://jitpack.io")
        }
    }

    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": {
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
                }
            }
        }
    }
}

include("magiclib-core")
include("magiclib-core:common")
include("magiclib-core:fabric")
include("magiclib-core:forge")
include("magiclib-core:neoforge")
include("magiclib-minecraft-api")
include("magiclib-minecraft-api:common")

def settings = new JsonSlurper().parseText(file("settings.json").text)

for (String module_name: settings.projects.keySet()) {
    String root_node_name = settings.projects.get(module_name).get(0)

    for(String v: settings.projects.get(module_name)) {
        String project_name = "${module_name}:${v}"
        include("${project_name}")

        ProjectDescriptor proj = project(":${project_name}")
        proj.setProjectDir(file("${module_name}/versions/${v}"))
        proj.setBuildFileName("../${root_node_name}/build.gradle")
    }
}
