import groovy.json.JsonBuilder

plugins {
    id("java-library")
    id("maven-publish")
    id("signing")
}

archivesBaseName("${project.mod_archives_base_name}-all")
group(project.mod_maven_group)
sourceCompatibility(JavaVersion.VERSION_1_8)
targetCompatibility(JavaVersion.VERSION_1_8)
version(project.parent.version)

def fabric_subprojects = project.parent.subprojects.findAll({
    it.name != "fabricWrapper"
})

fabric_subprojects.collect {
    evaluationDependsOn(":${it.name}")
}

jar {
    // disable cache
    outputs.upToDateWhen { false }

    dependsOn(fabric_subprojects.collect {
        it.tasks.remapJar
    })

    doFirst {
        delete fileTree("build/tmp/submods/META-INF/jars")

        copy {
            from {
                fabric_subprojects.collect {
                    it.remapJar.outputs.files
                }
            }

            into("build/tmp/submods/META-INF/jars")
        }
    }

    from("../LICENSE")
    from("build/tmp/submods")
}

processResources {
    // disable cache
    outputs.upToDateWhen { false }
    ArrayList<?> mc_condition = []
    ArrayList<?> jars = []

    fabric_subprojects.each({
        mc_condition.add("${it.minecraft_dependency}")
        jars.add(["file": "META-INF/jars/${project.mod_archives_base_name}-${it.minecraft_version}-${project.version}.jar"])
    })

    from("${rootDir}/icon.png") {
        into("assets/${project.mod_id}")
    }

    filesMatching("fabric.mod.json") {
        expand([
                "minecraft_dependency": new JsonBuilder(mc_condition),
                "mod_description"     : project.mod_description,
                "mod_homepage"        : project.mod_homepage,
                "mod_id"              : project.mod_id,
                "mod_license"         : project.mod_license,
                "mod_name"            : project.mod_name,
                "mod_version"         : project.version,
                "mod_sources"         : project.mod_sources,
                "sub_jars"            : new JsonBuilder(jars).toPrettyString(),
        ])
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        source("17")
        encoding("UTF-8")
        charSet("UTF-8")
        addStringOption("Xdoclint:none", "-quiet")
    }

    include("**/api/**")
}

publishing {
    publications {
        register("mavenJava", MavenPublication) {
            artifactId("${project.mod_id}")
            version("${rootProject.getMavenArtifactVersion()}")
            from(components.java)

            pom {
                name.set("${project.mod_name}")
                description.set("${project.mod_description}")
                url.set("${project.mod_sources}")

                licenses {
                    license {
                        name.set("${project.mod_license}")
                        url.set("${project.mod_license_url}")
                    }
                }

                developers {
                    developer {
                        id.set("${project.author_id}")
                        name.set("${project.author_name}")
                        email.set("${project.author_email}")
                    }
                }

                scm {
                    connection.set("scm:git:${project.mod_sources}.git")
                    developerConnection.set("scm:git:${project.mod_sources.replace("https", "ssh")}.git")
                    url.set("${project.mod_sources}")
                }
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            name("ProjectLocal")
            url("$rootDir/publish")
        }

        String mavenCentral_username = project.getOrDefault("secrets.mavenCentral.username", project.getENV().MAVEN_CENTRAL_USERNAME)
        String mavenCentral_password = project.getOrDefault("secrets.mavenCentral.password", project.getENV().MAVEN_CENTRAL_PASSWORD)

        if (mavenCentral_username && mavenCentral_password) {
            maven {
                name("MavenCentral")
                String releaseRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                String snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url(project.getVersionType() == "stanle" ? releaseRepoUrl : snapshotRepoUrl)

                credentials {
                    username(mavenCentral_username)
                    password(mavenCentral_password)
                }
            }
        }
    }
}

signing {
    String signingKey = project.getOrDefault("secrets.gpg.signingKey", project.getENV().SIGNING_PGP_KEY)
    String signingPassword = project.getOrDefault("secrets.gpg.signingPassword", project.getENV().SIGNING_PGP_PASSWORD)

    required {
        signingKey
    }

    useInMemoryPgpKeys(signingKey, signingPassword ? signingPassword : "")
    sign(publishing.publications.mavenJava)
}
