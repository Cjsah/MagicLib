plugins {
    id('java-library')
}
apply(plugin: "maven-publish")

setArchivesBaseName("${project.archives_base_name}")
setGroup(project.mod_maven_group)
setSourceCompatibility(JavaVersion.VERSION_1_8)
setTargetCompatibility(JavaVersion.VERSION_1_8)
setVersion(project.parent.version)

def fabric_subprojects = project.parent.subprojects.findAll({
    it.name != "fabricWrapper"
})

fabric_subprojects.collect {
    evaluationDependsOn(":${it.name}")
}

jar {
    // disable cache
    outputs.upToDateWhen { false }
    dependsOn(fabric_subprojects.collect {
        it.tasks.remapJar
    })

    doFirst {
        delete fileTree("build/tmp/submods/META-INF/jars")
        copy {
            from {
                fabric_subprojects.collect {
                    it.remapJar.outputs.files
                }
            }
            into("build/tmp/submods/META-INF/jars")
        }
    }

    from("../LICENSE")
    from("build/tmp/submods")
}

import groovy.json.JsonBuilder

processResources {
    // disable cache
    outputs.upToDateWhen { false }
    var jars = []

    fabric_subprojects.each({
        jars.add(["file": "META-INF/jars/${project.archives_base_name}-${it.minecraft_version}-${project.version}.jar"])
    })

    from("${rootDir}/icon.png") {
        into("assets/${project.mod_id}")
    }

    filesMatching("fabric.mod.json") {
        expand([
                "mod_description": project.mod_description,
                "mod_homepage"   : project.mod_homepage,
                "mod_id"         : project.mod_id,
                "mod_license"    : project.mod_license,
                "mod_name"       : project.mod_name,
                "mod_version"    : project.version,
                "mod_sources"    : project.mod_sources,
                "sub_jars"       : new JsonBuilder(jars).toPrettyString(),
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId(project.mod_id)
            groupId("${project.mod_maven_group}")
            version(rootProject.mavenArtifactVersion)
            from(components.java)
        }
    }

    repositories {
        mavenLocal()
        maven {
            url("$rootDir/publish")
        }
    }
}